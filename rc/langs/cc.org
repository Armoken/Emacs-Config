#+TITLE: CCPP
#+OPTIONS: toc:nil num:nil ^:nil

C/C++ configuration for Emacs

* Prerequisites
** System
   :PROPERTIES:
   :CUSTOM_ID: ccpp-system-prerequisites
   :END:

   #+NAME: ccpp-system-prerequisites
   #+CAPTION: System prerequisites for C/C++ packages
* Packages
  :PROPERTIES:
  :CUSTOM_ID: ccpp-packages
  :END:

  #+NAME: ccpp-packages
  #+CAPTION: Packages for C/C++
  - [[https://github.com/Sarcasm/irony-mode][irony]] :: A C/C++ minor mode for Emacs powered by libclang
  - [[https://github.com/hotpxl/company-irony-c-headers][company-irony-c-headers]] ::  Company mode backend for C/C++ header files with Irony
  - [[https://github.com/Sarcasm/company-irony][company-irony]] :: company-mode completion back-end for irony-mode
  - [[https://github.com/Sarcasm/flycheck-irony][flycheck-irony]] :: C, C++ and Objective-C support for Flycheck, using Irony Mode
  - [[https://github.com/ikirill/irony-eldoc][irony-eldoc]] :: irony-mode support for eldoc-mode
  - [[https://github.com/sonatard/clang-format][clang-format]] :: Clang-format emacs integration for use with C/Objective-C/C++
  - [[https://github.com/ludwigpacifici/modern-cpp-font-lock][modern-cpp-font-lock]] :: C++ font-lock for Emacs
  - [[https://github.com/atilaneves/cmake-ide][cmake-ide]] :: Use Emacs as a C/C++ IDE
* Configuration
   #+BEGIN_SRC emacs-lisp :noweb yes
     (use-package cc-mode
         :mode (("\\.h\\'"   . c-mode)
                ("\\.c\\'"   . c-mode)
                ("\\.hpp\\'" . c++-mode)
                ("\\.cpp\\'" . c++-mode))
         :init
         (progn
             <<cc-configuration>>

             <<c-configuration>>
             <<c++-configuration>>
          )
         :config
         (progn
             (setq c-basic-offset       4
                   c-default-style      '((c-mode    . "k&r")
                                          (c++-mode  . "stroustrup")
                                          (java-mode . "java"))
                   c-doc-comment-style  '((java-mode . javadoc)
                                          (c-mode    . javadoc)
                                          (c++-mode  . javadoc)))))
   #+END_SRC

** CC configuration
   #+NAME: cc-configuration
   #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
     (use-package clang-format
         :ensure t
         :commands (clang-format clang-buffer clang-format-region))

     <<irony-use-package>>
     <<gtags-use-package>>
     <<ccls-use-package>>
     <<cmake-ide-use-package>>

     (defun cc-hook ()
         (hs-minor-mode)

         (setq
          ;; Setup minor mods if any component need it
          conf-variable-for-cc (list cc-eldoc-plugin
                                     cc-syntax-check-plugin
                                     cc-autocompletion-plugin))

         (local-set-keys '(("C-c TAB" . hs-toggle-hiding)
                           ("C-c C-r" . clang-format-region)))

         (if (member "irony" conf-variable-for-cc)
                 (irony-setup))
         (if (member "gtags" conf-variable-for-cc)
                 (gtags-setup))
         (if (member "ccls" conf-variable-for-cc)
                 (ccls-setup))

         ;; Autocompletion setup
         (start-company-mode)

         (make-local-variable 'company-backends)
         (funcall (pcase cc-autocompletion-plugin
                      ("irony"  'irony-autocompletion-setup)
                      ("gtags"  'gtags-autocompletion-setup)
                      ("ccls"   'ccls-autocompletion-setup)))

         ;; Syntax check setup
         (funcall (pcase cc-syntax-check-plugin
                      ("irony"  'irony-syntax-check-setup)
                      ("gtags"  'gtags-syntax-check-setup)
                      ("ccls"   'ccls-syntax-check-setup)))

         ;; Eldoc setup
         (funcall (pcase cc-eldoc-plugin
                      ("irony"  'irony-eldoc-setup)
                      ("gtags"  'gtags-eldoc-setup)
                      ("ccls"   'ccls-eldoc-setup)))
         ;; Debug support
         (require 'dap-lldb)
         (dap-mode 1))

     (add-hook 'c-mode-hook #'cc-hook)
     (add-hook 'c++-mode-hook #'cc-hook)
   #+END_SRC

*** Irony
     #+NAME: irony-use-package
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (use-package irony
           :ensure t
           :diminish irony-mode "[I]"
           :init
           (progn
               <<irony-autocompletion>>
               <<irony-syntax-analytics>>
               <<irony-eldoc>>

               (defun irony-setup ()
                   (setq irony-server-install-prefix
                         (concat user-emacs-directory
                                 "/contrib/servers/irony"))

                   (irony-mode))))
     #+END_SRC

**** Autocompletion
     #+NAME: irony-autocompletion
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (use-package company-irony
           :ensure t)
       (use-package company-irony-c-headers
           :ensure t)

       (setq company-irony-ignore-case 'smart)

       ;; Use compilation database first, clang_complete as fallback.
       (setq-default irony-cdb-compilation-databases
                     '(irony-cdb-libclang
                       irony-cdb-clang-complete))

       (defun irony-autocompletion-setup ()
           (define-key irony-mode-map [remap completion-at-point]
               'irony-completion-at-point-async)
           (define-key irony-mode-map [remap complete-symbol]
               'irony-completion-at-point-async)
           (irony-cdb-autosetup-compile-options)

           ;; (optional) adds CC special commands to
           ;; `company-begin-commands' in order to
           ;; trigger completion at interesting places, such as after
           ;; scope operator std::|
           (company-irony-setup-begin-commands)

           (push '(company-irony-c-headers company-irony) company-backends))
     #+END_SRC

**** Syntax analytics
     #+NAME: irony-syntax-analytics
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (use-package flycheck-irony
           :ensure t)

       (defun irony-syntax-check-setup ()
           (flycheck-mode)
           (flycheck-irony-setup))
     #+END_SRC

**** Eldoc
     #+NAME: irony-eldoc
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (use-package irony-eldoc
           :ensure t)

       (defun irony-eldoc-setup ()
           (eldoc-mode)
           (irony-eldoc))
     #+END_SRC

*** GTags
     #+NAME: gtags-use-package
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       <<gtags-autocompletion>>
       <<gtags-syntax-analytics>>
       <<gtags-eldoc>>

       (defun gtags-setup ()
           (setq-local imenu-create-index-function #'ggtags-build-imenu-index)

           (ggtags-mode))
     #+END_SRC

**** Autocompletion
     #+NAME: gtags-autocompletion
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (defun gtags-autocompletion-setup ()
           (push '(company-gtags) company-backends))
     #+END_SRC

**** Syntax analytics
     #+NAME: gtags-syntax-analytics
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (defun gtags-syntax-check-setup ()
           (flycheck-mode))
     #+END_SRC

**** Eldoc
     #+NAME: gtags-eldoc
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (defun gtags-eldoc-setup ()
           (eldoc-mode))
     #+END_SRC

*** CCLS
     #+NAME: ccls-use-package
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (use-package ccls
           :ensure t
           :diminish ccls-code-lens-mode
           :init
           (progn
               <<ccls-autocompletion>>
               <<ccls-syntax-analytics>>
               <<ccls-eldoc>>

               (defun ccls-setup ()
                   (setq ccls-executable (executable-find "ccls"))

                   ;; Log file
                   (setq ccls-extra-args '("--log-file=/tmp/ccls-9999.log"))
                   (setq ccls-extra-init-params
                         '(
                           :index       (:comments 2)
                           :completion  (:detailedLabel t)))

                   ;; (setq ccls-sem-highlight-method 'overlay)


                   (local-set-keys '(("M-."     . lsp-ui-peek-find-definitions)
                                     ("M-,"     . xref-pop-marker-stack)
                                     ("M-?"     . lsp-ui-peek-find-references)
                                     ("C-M-."   . xref-find-apropos)
                                     ("C-c h i" . lsp-ui-imenu)))

                   (lsp))))
     #+END_SRC

**** Autocompletion
     #+NAME: ccls-autocompletion
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (defun ccls-autocompletion-setup ()
           (setq company-backends (delete 'company-lsp company-backends))
           (push '(company-lsp :with company-yasnippet) company-backends))
     #+END_SRC

**** Syntax analytics
     #+NAME: ccls-syntax-analytics
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (defun ccls-syntax-check-setup ()
           (flycheck-mode))
     #+END_SRC

**** Eldoc
     #+NAME: ccls-eldoc
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (defun ccls-eldoc-setup ()
           (eldoc-mode))
     #+END_SRC

*** CMake IDE
     #+NAME: cmake-ide-use-package
     #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
       (use-package cmake-ide
           :ensure t
           :commands (cmake-ide-setup))
     #+END_SRC

** C configuration
   #+NAME: c-configuration
   #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
     (defun c-hook ()
         (setq clang-format-style               "webkit"
               flycheck-clang-language-standard "c99"
               irony-additional-clang-options   '("-Wall"
                                                  "-Wextra")))
     (add-hook 'c-mode-hook #'c-hook)
   #+END_SRC

** C++ configuration
   #+NAME: c++-configuration
   #+BEGIN_SRC emacs-lisp :tangle no :noweb yes
     (use-package modern-cpp-font-lock
         :ensure t
         :diminish modern-c++-font-lock-mode
         :commands (modern-c++-font-lock-mode))
     (add-hook 'c++-mode-hook #'modern-c++-font-lock-mode)

     (defun c++-hook ()
         (setq clang-format-style               "webkit"
               flycheck-clang-language-standard "c++17"
               irony-additional-clang-options   '("-Wall"
                                                  "-Wextra")))
     (add-hook 'c++-mode-hook #'c++-hook)
   #+END_SRC
